# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgbase=calamares-oem
pkgname=("calamares-oem-modules" "calamares-oem-sx-settings")
pkgver=20170910
pkgrel=2
arch=('any')
_repo=project-sx
_commit=64ebc7c18bab121e918a8b867f1dbe83328cba41
url="https://github.com/philmmanjaro/$_repo"
license=('GPL3')
makedepends=('git')
source=("git+$url.git" 'cleanupoem.service')
md5sums=('SKIP'
         '89410e5af734c7585b58530cba9d9685')

pkgver() {
	date +%Y%m%d
}

package_calamares-oem-modules() {
	pkgdesc="Manjaro OEM Modules"
	depends=('calamares' 'calamares-oem-settings')
        install=calamares-oem-modules.install
	cd "${_repo}/${pkgbase}"
	install -d "${pkgdir}"/usr/lib/calamares
	cp -r modules "${pkgdir}"/usr/lib/calamares
	install -Dm644 postinstall-settings/settings.conf "${pkgdir}"/etc/calamares/settings.conf
	install -Dm644 postinstall-settings/welcome.conf "${pkgdir}"/etc/calamares/modules/welcome.conf
	install -Dm644 postinstall-settings/users.conf "${pkgdir}"/etc/calamares/modules/users.conf
	install -Dm644 postinstall-settings/finished.conf "${pkgdir}"/etc/calamares/modules/finished.conf
	install -Dm644 cleanupoem.service "${pkgdir}"/opt/calamares/cleanupoem.service
}

package_calamares-oem-sx-settings() {
	pkgdesc="Manjaro OEM SX-Settings"
	provides=('calamares-oem-settings')
	cd "${_repo}/${pkgbase}"
	install -d "${pkgdir}"/etc
	cp -r oemskel "${pkgdir}"/etc
}
